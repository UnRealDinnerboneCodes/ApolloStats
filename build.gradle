plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id "maven-publish"
    id 'java-library'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)


def ENV = System.getenv()
version = '1.0.0'


configurations {
    api
    implementation.extendsFrom api
    apiElements.extendsFrom api

    shade
    implementation.extendsFrom shade
    shadow.extendsFrom shade

    commonLib
    coreImplementation.extendsFrom commonLib
    webImplementation.extendsFrom commonLib
    discordImplementation.extendsFrom commonLib
    shade.extendsFrom commonLib

    webLib
    webImplementation.extendsFrom webLib
    shade.extendsFrom webLib

    discordLib
    discordImplementation.extendsFrom discordLib
    shade.extendsFrom discordLib
}

sourceSets {
    web
    core
    discord
}

repositories {
    mavenCentral()
    maven { url "https://maven.unreal.codes/releases"}
}


dependencies {

    commonLib("com.unrealdinnerbone:PostgressLib:2.2.0")
    commonLib("com.unrealdinnerbone:UnRealConfig2:1.3.0")
    commonLib("com.unrealdinnerbone:UnRealLib:9.0.0")
    commonLib("org.apache.commons:commons-text:1.10.0")
    commonLib("net.kyori:adventure-text-serializer-gson:4.16.0")
    commonLib("net.kyori:adventure-text-serializer-plain:4.16.0")
    commonLib("com.google.guava:guava:33.0.0-jre")

    webLib("io.javalin:javalin:6.1.3")
    webImplementation sourceSets.core.output

    discordImplementation sourceSets.core.output

    shade("org.fusesource.jansi:jansi:2.4.0")
    shade("ch.qos.logback:logback-classic:1.4.14")
    shade("org.reflections:reflections:0.9.11")

    shade sourceSets.core.output
    shade sourceSets.web.output
    shade sourceSets.discord.output

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

jar {
    setArchiveBaseName(project.property("name"))
    manifest {
        attributes(
                'Main-Class': "com.unrealdinnerbone.apollostats.StatsLauncher",
                "Implementation-Version": version
        )
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication).from(components.java)
    }
    repositories {
        maven {
            url "https://maven.unreal.codes/releases"
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}